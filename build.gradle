plugins {
    id 'java'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'com.github.ben-manes.versions' version '0.20.0'
}

def compatibilityVersion = 1.8
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // process Project Lombok annotations
    def lombok = 'org.projectlombok:lombok:1.18.4'
    compileOnly lombok
    annotationProcessor lombok

    // WekaNet dependencies
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation 'nz.ac.waikato.cms.weka:weka-stable:3.8.3'

    // test dependencies
    testImplementation 'junit:junit:4.13-beta-1'
}

javadoc {
    title 'WekaNet: Weka Artificial Neural Network ' + version + ' API'

    options {
        author true
        linkSource true
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://docs.oracle.com/javase/8/javafx/api/'
        links 'http://weka.sourceforge.net/doc.stable/'
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

nexus {
    sign = false
    repositoryUrl = (System.getenv('NEXUS_URL') ?: nexusURL) + 'releases'
    snapshotRepositoryUrl = (System.getenv('NEXUS_URL') ?: nexusURL) + 'snapshots'
}

plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

def compatibilityVersion = 1.8
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion
version '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    // process Project Lombok annotations
    def lombok = 'org.projectlombok:lombok:1.18.10'
    compileOnly lombok
    annotationProcessor lombok

    // WekaNet dependencies
    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'nz.ac.waikato.cms.weka:weka-stable:3.8.3'

    // test dependencies
    testImplementation 'junit:junit:4.13-rc-1'
}

def static javaAPI() {
    switch(JavaVersion.current()) {
        case JavaVersion.VERSION_1_8:
            return 'https://docs.oracle.com/javase/8/docs/api/'
        case JavaVersion.VERSION_1_9:
            return 'https://docs.oracle.com/javase/9/docs/api/'
        case JavaVersion.VERSION_11:
            return 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        default:
            throw new GradleException('Unsupported Java version')
    }
}

javadoc {
    title 'WekaNet: Weka Artificial Neural Network ' + version + ' API'

    options {
        author true
        linkSource true
        links javaAPI()
        links 'http://weka.sourceforge.net/doc.stable/'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
